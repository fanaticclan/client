// Project: Music Player
// License: Creative Commons â€” CC0 1.0 Universal
// Version: 1.0
// Author: Nyne
//
// ***********************************************************************************
// * CC0 1.0 Universal (CC0 1.0)                                                     *
// * Public Domain Dedication                                                        *
// * This is a human-readable summary of the Legal Code.                             *
// *                                                                                 *
// *    The person who associated a work with this deed has dedicated the work to    *
// *    the public domain by waiving all of his or her rights to the work            *
// *    worldwide under copyright law, including all related and neighboring         *
// *    rights, to the extent allowed by law.                                        *
// *                                                                                 *
// *    You can copy, modify, distribute and perform the work, even for commercial   *
// *    purposes, all without asking permission.                                     *
// *                                                                                 *
// *    <http://creativecommons.org/publicdomain/zero/1.0/>                          *
// *                                                                                 *
// ***********************************************************************************

playasong = []

sleep 1 [
    playasong = [
       local _playlist _track
        loopfiles f "faned/music" "" [
            if (> (indexof [.mid .mp3 .ogg .wav .wma] (substr $f (- (strlen $f) 4))) -1) [
                _playlist = (concat (getalias _playlist) [[@@f]])
            ]
        ]
        _track = (at $_playlist (rnd (listlen $_playlist)))
        music [../faned/music/@_track] playasong
        if (> $musicvol 1) [echo "^f9FanEd^f7::music:^f4" $_track]
    ]; playasong
]

createine = [if (! (getalias $arg1)) [$arg1 = $arg2]]
createine _music_path "faned/music/"
createine _musicvol $musicvol
createine _muted 1

newgui music [
    guistayopen [
        guilist [
            guitext "Set music directory: " "info.jpg"
            guifield _music_path -68
        ]
        guistrut 1
        guitext (concat "Current music stored @" $_music_path) "info.jpg"
        guistrut 1
        _music_files = ""
        loopfiles f $_music_path "" [
            if (> (indexof [.mid .mp3 .ogg .wav .wma] (substr $f (- (strlen $f) 4))) -1) [
                _music_files = (concat (getalias _music_files) [[@@f]])
            ]
        ]
        _music_files = (sortlist $_music_files x y [<=s $x $y])
        guilist [
            guilist [
                looplist f $_music_files [
                    guibutton (concat "Play^f9" $f) [
                        music (concatword "../" $_music_path "/" @f) playasong
                        echo (concat "^f9FanEd^f7::music:^f4" @f)
                    ]
                ]
            ]
            guistrut 3
            guibar
            guistrut 3
            guilist [
                guilist [
                    guilist [
                        if (> $musicvol 1) [guitext (concatword "^f9" (loopconcatword i (rnd 18) [ result ":" ])) 0]
                        guistrut 1
                        guitext "Music Volume:"
                        guislider musicvol
                        guistrut 1
                        guitext "Sound Volume:"
                        guislider soundvol
                        guistrut 1
                        guilist [
                            if $_muted [
                                guibutton "Mute" [_muted = 0; musicvol $_musicvol] checkbox_on
                            ] [
                                guibutton "Mute" [_muted = 1; _musicvol = $musicvol; musicvol 1] checkbox_off
                            ]
                        ]
                    ]
                ]
            ]
        ]
    ]
] "Music"

showmusiccontrol = [showgui music]

