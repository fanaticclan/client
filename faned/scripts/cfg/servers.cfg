// Fanatic Edition: servers.cfg

createine = [if (=s (getalias $arg1) "") [$arg1 = $arg2]]

createine _fs_servers 5
looplist p "serv ip port" [
    loop i 15 [
        createine [_fs_@@p@i]
    ]
]

createine serverlistmax 17

newgui servers [
    guistayopen [
        guiservers [
            guilist [
                guibutton "update server list" "updatefrommaster"
                guibar
                guicheckbox "auto-update" autoupdateservers
                guistrut 1
                if $autoupdateservers [
                    _servpingrate = $servpingrate
                    guifield _servpingrate 5 [servpingrate $_servpingrate]
                    guitext " ms" 0
                    guibar
                ]
                guicheckbox "auto-sort" autosortservers
                if (= $autosortservers 0) [
                    guibar
                    guibutton "sort" "sortservers"
                ]
                guibar
                guicheckbox "auto-auth" autoauth
                guibar
                guibutton "reconnect" [reconnect]
                guistrut 20
                guibutton "> configure" [showgui configservers] 0
            ]
            guibar
            loop i $_fs_servers [
                if (&& [!=s $_fs_serv@i ""] [!=s $_fs_ip@i ""]) [
                    guibutton (concat "^f9#!^f7" $_fs_serv@i) [
                        connect $_fs_ip@i $_fs_port@i
                    ] server
                ]
            ]
        if $_fs_servers guibar
        ] $serverlistmax
    ]
] "Servers" [initservers]

newgui configservers [
    guinoautotab [
        guistayopen [
            guilist [
                guilist [
                    guititle "Server Name"
                    guibar
                    loop i $_fs_servers [
                        guilist [
                            guibutton "" [
                                looplist p "serv ip port" [
                                    [_fs_@[p]@@@i] = ""
                                ]
                            ] exit
                            guifield _fs_serv@i 20
                        ]
                    ]
                ]
                guibar
                guilist [
                    guititle "Address"
                    guibar
                    loop i $_fs_servers [
                        guifield [_fs_ip@i] 20 [
                            if (=s $_fs_ip@i "") [
                                _fs_ip@@i = (? (isconnected) (connectedip))
                            ]
                        ]
                    ]
                ]
                guibar
                guilist [
                    guititle "Port"
                    guibar
                    loop i $_fs_servers [
                        guifield [_fs_port@i] 5 [
                            if (=s $_fs_port@i "") [
                                _fs_port@@i = (? (isconnected) (connectedport) 28785)
                            ]
                        ]
                    ]
                ]
            ]
            guititle
            guilist [
                if (< $_fs_servers 15) [
                    guibutton "Add Row" [
                        _fs_servers = (min 15 (+ $_fs_servers 1))
                    ]
                ]
                guispring
                if (> $_fs_servers 5) [
                    guibutton "Remove Row" [
                        _fs_servers = (max 5 (- $_fs_servers 1))
                    ]
                ]
            ]
        ]
    ]
] "Config"

