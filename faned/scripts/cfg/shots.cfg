// Fanatic Edition: shots.cfg

createine = [if (! (getalias $arg1)) [$arg1 = $arg2]]

createine _shotcolor_red 34
createine _shotcolor_green 34
createine _shotcolor_blue 34
createine _shotwidth 30

newgui shots [
    guistayopen [
        guinoautotab [
            guilist [
                _ss = $shotpart
                guiradio "none (^f90^f7) "      _ss 0 [shotpart 0]
                guiradio "laser (^f91^f7) "     _ss 1 [shotpart 1; shotduration 1500; shotwidth 0.6; _shotwidth = 30]
                guiradio "lightning (^f92^f7) " _ss 2 [shotpart 2; shotduration  500; shotwidth 0.6; _shotwidth = 30]
                guiradio "smoke (^f93^f7) "     _ss 3 [shotpart 3; shotduration  500; shotwidth 0.6; _shotwidth = 30]
                guiradio "spin (^f94^f7) "      _ss 4 [shotpart 4; shotduration  500; shotwidth 0.2; _shotwidth = 10]
            ]
            guistrut 1
            _set_shotcolor = [shotcolor (+ (* (+ (* $_shotcolor_red 256) $_shotcolor_green) 256) $_shotcolor_blue)] 
            guilist [
                guitext "shotcolor:"
                guistrut 1
                if $shotcolorrainbow [ 
                    guicheckbox "^fRRainbow" shotcolorrainbow 
                ] [ 
                    guicheckbox "Rainbow" shotcolorrainbow 
                ]
                guistrut 1
                guicheckbox "Team" shotcolorteam
            ]
            if (&& [! $shotcolorrainbow] [! $shotcolorteam]) [
                guistrut 1
                guislider _shotcolor_red   0 255 [_set_shotcolor] (+ (* (+ (* $_shotcolor_red 256) $_shotcolor_green) 256) $_shotcolor_blue) 
                guislider _shotcolor_green 0 255 [_set_shotcolor] (+ (* (+ (* $_shotcolor_red 256) $_shotcolor_green) 256) $_shotcolor_blue) 
                guislider _shotcolor_blue  0 255 [_set_shotcolor] (+ (* (+ (* $_shotcolor_red 256) $_shotcolor_green) 256) $_shotcolor_blue) 
            ]
            guistrut 1
            guitext "shotduration:" 
            guislider shotduration
            guitext "shotgravity:" 
            guislider shotgravity
            guitext "shotwidth:" 
            guislider _shotwidth 0 500 [shotwidth (divf $_shotwidth 50)]
            guistrut 1
            guilist [
                guicheckbox "muzzleflash"  [muzzleflash]
                guistrut 1
                guicheckbox "muzzlelight"  [muzzlelight]
                guistrut 1
                guicheckbox "sparks" [shotsparks]
            ]
        ]
    ]
] "Shots"

