// Project: Master GUI
// License: Creative Commons â€” CC0 1.0 Universal
// Version: 1.1
// Author: Nyne
//
// ***********************************************************************************
// * CC0 1.0 Universal (CC0 1.0)                                                     *
// * Public Domain Dedication                                                        *
// * This is a human-readable summary of the Legal Code.                             *
// *                                                                                 *
// *    The person who associated a work with this deed has dedicated the work to    *
// *    the public domain by waiving all of his or her rights to the work            *
// *    worldwide under copyright law, including all related and neighboring         *
// *    rights, to the extent allowed by law.                                        *
// *                                                                                 *
// *    You can copy, modify, distribute and perform the work, even for commercial   *
// *    purposes, all without asking permission.                                     *
// *                                                                                 *
// *    <http://creativecommons.org/publicdomain/zero/1.0/>                          *
// *                                                                                 *
// ***********************************************************************************

newgui master [
    guistayopen [
        guilist [
            guitext "Set privilege: " "info.jpg"
            if (ismaster (getclientnum)) [
                guibutton "relinquish master " [setmaster 0]
                guibutton "equality " [looplist i $listclients [setmaster 1 $i]]
                guibutton "specall " [looplist i $listclients [spectator 1 $i]]
                guibutton "unspecall " [looplist i $listclients [spectator 0 $i]]
            ] [
                guibutton "claim master " [setmaster 1]
            ]
        ]
        guilist [
            guitext "Set mastermode: " "info.jpg"
            mm = (getmastermode)
            guiradio "^f7open (0) " mm 0 [mastermode 0]
            guiradio "^f7veto (1) " mm 1 [mastermode 1]
            guiradio "^f7locked (2) " mm 2 [mastermode 2]
            guiradio "^f7private (3) " mm 3 [mastermode 3]
        ]
        guistrut 1
        looplist cn $listclients [
            guitext (concatword "client: ^f7(^f9" $cn "^f7) ^f9" (getclientname $cn) "^f7 ")
            guilist [
                guibutton "follow (goto) " [
                if (= $editing 0) [edittoggle]
                    _goto_loop = [
                        goto @@cn
                        if (= $editing 1) [
                            sleep 0 [_goto_loop]
                        ] [_goto_loop = 0]
                    ]
                    _goto_loop
                    echo (concatword "^f9FanEd^f7::goto: Following " (getclientname @@cn) " (" @@cn "); hit E (edittoggle) to stop.")
               ]
                guibutton "follow (spec) " [
                    spectator 1
                    sleep 1000 [
                        follow @@cn
                        echo (concatword "^f9FanEd^f7::spec: Following " (getclientname @@cn) " (" @@cn "); type /spectator 0 to stop.")
                    ]
                ]
                guibutton "goto " [goto @cn]
                if (=s (getclientteam $cn) "good") [guibutton "switch evil " [setteam @cn "evil"]] [guibutton "switch good " [setteam @cn "good"]]
                if ((isspectator $cn)) [guibutton "unspectate " [spectator 0 @cn]] [guibutton "spectate " [spectator 1 @cn]]
                if (ismaster $cn) [guibutton "take master " [setmaster 0 @cn]] [guibutton "give master " [setmaster 1 @cn]]
            ]
        ]
    ]
] "Master"

